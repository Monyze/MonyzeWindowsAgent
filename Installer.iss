; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Monyze Windows Agent"
#define MyAppVersion "1.0.3"
#define MyAppPublisher "Monyze, Inc."
#define MyAppURL "https://monyze.ru/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{495BE497-94EA-4B83-BD1A-AF8BA9321665}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=Installer
OutputBaseFilename=MonyzeWindowsAgent-{#MyAppVersion}
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Files]
Source: "MonyzeWindowsAgent\bin\Release\MonyzeWindowsAgent.exe"; DestDir: "{app}"; Flags: ignoreversion;
Source: "MonyzeWindowsAgent\bin\Release\MonyzeWindowsAgent.exe.config"; DestDir: "{app}"; Flags: ignoreversion;
Source: "MonyzeWindowsAgent\bin\Release\log4net.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "MonyzeWindowsAgent\bin\Release\OpenHardwareMonitorLib.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "monyze_config.ini"; DestDir: "{app}"; Flags: ignoreversion;

[Run]
Filename: {sys}\sc.exe; Parameters: "stop ""MonyzeAgent""" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "delete ""MonyzeAgent""" ; Flags: runhidden
Filename: {app}\MonyzeWindowsAgent.exe; Parameters: "--install" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "start ""MonyzeAgent""" ; Flags: runhidden

[UninstallRun]
Filename: {sys}\sc.exe; Parameters: "stop ""MonyzeAgent""" ; Flags: runhidden;
Filename: {sys}\timeout.exe; Parameters: "1" ; Flags: runhidden;
Filename: {app}\MonyzeWindowsAgent.exe; Parameters: "--uninstall" ; Flags: runhidden

[Code]
var
  CustomQueryPage: TInputQueryWizardPage;

procedure AddCustomQueryPage();
begin
  CustomQueryPage := CreateInputQueryPage(
    wpWelcome,
    'Monyze monitoring agent',
    '',
    '');

  { Add items (False means it's not a password edit) }
  CustomQueryPage.Add('User ID:', False);
  CustomQueryPage.Add('Device ID:', False);
end;

procedure InitializeWizard();
begin
  AddCustomQueryPage();
end;

function GetUserID(param: String): String;
begin
  Result := Trim(CustomQueryPage.Values[0]);
end;

function GetDeviceID(param: String): String;
begin
  Result := Trim(CustomQueryPage.Values[1]);
end;

[INI]
Filename: {app}\monyze_config.ini; Section: "MonyzeWindowsAgent"; Key: "user_id"; String: {code:GetUserID}
Filename: {app}\monyze_config.ini; Section: "MonyzeWindowsAgent"; Key: "device_id"; String: {code:GetDeviceID}